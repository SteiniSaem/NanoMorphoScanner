%{
	import java.io.*;
	import java.util.HashMap;
	import java.util.Arrays;
%}

%token <sval> NAME, VAR, OPNAME, LITERAL, IF, ELSIF, ELSE, RETURN, WHILE, ERROR
%type<obj> program, start, function, expr_star, expr_plus, decl_star, decl_plus, decl_atom, name_star, name_plus, expr, binopexpr, smallexpr, ifexpr, ifexpr_elif, ifexpr_else, body
%type<obj> expr_line, expr_line_plus, expr_line_star

%%

start
	:	program					{NanoMorphoFinalCodeGenerator.generateProgram(name, (Object[]) $1);}
	;

program
	:	function program		{$$ = append($2, $1);}
	|	function				{$$ = $1;}
	;

function
	:							{
									argCount = 0;
									varCount = 0;
									varTable = new HashMap<String,Integer>();
								}
		NAME '(' name_star ')' '{' decl_star expr_line_star '}'
								{
									$$ = new Object[]{$2, argCount, varCount, $8};
								}
	;

expr_line_star
	:	expr_line_plus			{$$ = $1;}
	|							{$$ = new Object[0];}
	;

expr_line_plus
	:	expr_line_plus
			expr_line			{$$ = append($1, $2);}
	|	expr_line				{$$ = new Object[]{$1};}
	;

expr_line
	:	expr ';'				{$$ = $1;}
	;

expr_star
	:	expr_plus				{$$ = $1;}
	|							{$$ = new Object[0];}
	;

expr_plus
	:	expr_plus ',' expr		{$$ = append($1, $3);}
	|	expr 					{$$ = new Object[]{$1};}
	;

decl_star
	:	decl_plus				{$$ = $1;}
	|							{$$ = new Object[0];}
	;

decl_plus
	:	decl_plus decl_atom		{$$ = append($1, $2);}
	|	decl_atom				{$$ = new Object[]{$1};}
	;

decl_atom
	:	VAR name_plus ';'		{
									for (Object name : (Object[])($2))
									{
										addVar((String) name);
									}
									$$ = $2;
								}
	;

name_star
	:	name_plus				{$$ = $1;}
	|							{$$ = new Object[0];}
	;

name_plus
	:	name_plus ',' NAME 		{$$ =  append($1, $3);}
	|	NAME 					{$$ = new Object[]{$1};}
	;

expr
	:	RETURN expr 			{$$ = new Object[]{"RETURN", $2};}
	|	NAME '=' expr 			{$$ = new Object[]{"STORE", findVar($1), $3};}
	|	binopexpr				{$$ = $1;}
	;

binopexpr
	:	smallexpr
	| 	binopexpr
			OPNAME
			smallexpr			{$$ = new Object[]{"CALL", $2, new Object[]{$1, $3}};}
	;

smallexpr
	:	NAME 	 				{$$ = new Object[]{"FETCH", findVar($1)};}
	|	NAME '(' expr_star ')'	{$$ = new Object[]{"CALL", $1, $3};}
	|	OPNAME smallexpr 		{$$ = new Object[]{"CALL", $1, $2};}
	|	LITERAL					{$$ = new Object[]{"LITERAL", $1};}
	|	'(' expr ')'			{$$ = $2;}
	|	ifexpr 					{$$ = $1;}
	|	WHILE '(' expr ')' body	{$$ = new Object[]{"WHILE", $3, $5};}
	;

ifexpr
	:	IF
			'('
			expr
			')'
			body
			ifexpr_elif
								{$$ = new Object[]{"IF", $3, $5, $6};}
	;

ifexpr_elif
	:	ELSIF
			'('
			expr
			')'
			body
			ifexpr_elif
								{$$ = new Object[]{"IF", $3, $5, $6};}
	|	ifexpr_else				{$$ = $1;}
	|							{$$ = new Object[]{"BODY", new Object[0]};}
	;

ifexpr_else
	:	ELSE body				{$$ = new Object[]{"IF", new Object[] {"LITERAL", "true"}, $2, null};}
	;

body
	:	'{' expr_line_star '}'		{$$ = new Object[]{"BODY", $2};}
	;


%%

	static private String name;
	private int argCount;
	private int varCount;
	private HashMap<String,Integer> varTable;
	private NanoMorphoLexer lexer;
	private int last_token_read;

	private Object[] append(Object arr, Object x)
	{
		return append((Object[]) arr, x);
	}
	private Object[] append(Object[] arr, Object x)
	{
		Object[] ret = Arrays.copyOf(arr, arr.length+1);
		ret[ret.length-1] = x;
		return ret;
	}

	private void addVar( String name )
	{
		if( varTable.get(name) != null )
			yyerror("Variable "+name+" already exists");
		varTable.put(name,varCount++);
	}

	private int findVar( String name )
	{
		Integer res = varTable.get(name);
		if( res == null )
			yyerror("Variable "+name+" does not exist");
		return res;
	}

	private int yylex()
	{
		int yyl_return = -1;
		try
		{
			yylval = null;
			last_token_read = yyl_return = lexer.yylex();
			if( yylval==null )
				yylval = new ParserVal(Parser.yyname[yyl_return]);
		}
		catch (IOException e)
		{
			System.err.println("IO error: "+e);
		}
		return yyl_return;
	}

	public void yyerror( String error ) {
		System.err.println("Error: "+error + " Line: " + lexer.getLine());
		System.out.println("Token:  " + Parser.yyname[last_token_read]);
		System.exit(1);
	}

	public Parser( Reader r ) throws Exception
	{
		lexer = new NanoMorphoLexer(r,this);
	}
